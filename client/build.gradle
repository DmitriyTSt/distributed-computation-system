plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm'
    id "com.google.protobuf"
    id 'idea'
    id 'edu.sc.seis.launch4j' version '2.5.0'
}

group 'ru.dmitriyt.dcs.client'
version rootProject.ext.dcsVersion

repositories {
    mavenCentral()
}

jar {
    manifest {
        attributes 'Implementation-Title': 'DCS_Client',
                'Implementation-Version': project.version,
                'Main-Class': 'ru.dmitriyt.dcs.client.MainKt'
    }
    archiveFileName.set("dcs-${project.name}.jar")
    from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
}

launch4j { // used for windows
    mainClassName = 'ru.dmitriyt.dcs.client.MainKt'
    headerType = 'console'
    bundledJrePath = 'jre'
    bundledJre64Bit = true
    jreMinVersion = '8'
}

task packageExecutableDistribution(type: Zip) {
    archiveFileName = "dcs-client.zip"
    destinationDirectory = file("$buildDir/distExecutable")

    from "$buildDir/launch4j"
}

task addJreToDistributable(type: Copy) {
    from zipTree("resources/desktop-deployment/jre.zip")
    destinationDir = file("$buildDir/launch4j")
}

packageExecutableDistribution.dependsOn createExe
packageExecutableDistribution.dependsOn addJreToDistributable

dependencies {
    implementation "com.github.DmitriyTSt:dcs-core:${rootProject.ext.coreVersion}"
    implementation project(":core")
    implementation project(":proto")

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.2'
    implementation "io.perfmark:perfmark-api:0.24.0"
}